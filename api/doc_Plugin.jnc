//.............................................................................
//
// Ninja document model: plugins (sessions and protocols)
//

import "doc_PluginHost.jnc"
import "doc_Storage.jnc"

namespace doc {

//.............................................................................

class Plugin
{
	PluginHost* readonly m_pluginHost;

	construct (PluginHost* host)
	{
		m_pluginHost = host;
	}
	
	virtual void load (Storage* storage)
	{		
	}

	virtual void save (Storage* storage)
	{		
	}

	virtual void updateProperties ()
	{		
	}

	virtual bool applyProperties () throws
	{
		return true;
	}

	virtual void restoreDefaultProperties ()
	{		
	}

	abstract size_t transmit (
		void const* p,
		size_t size
		) throws;

	size_t transmitString (char const* s) throws
	{
		return try transmit (s, strlen (s));
	}	

	writeLog (
		uint_t recordCode,
		void const* p,
		size_t size
		)
	{
		m_pluginHost.m_log.m_writer.write (recordCode, p, size);
	}

	writeLog (uint_t recordCode)
	{
		m_pluginHost.m_log.m_writer.write (recordCode, null, 0);
	}

	gui.Action* createAction (
		char const* text,
		gui.Icon* icon = null,
		bool isCheckable = false,
		bool isChecked = false
		);

	gui.Action* createAction (
		char const* text,
		char const* iconFileName,
		bool isCheckable = false,
		bool isChecked = false		
		)
	{
		return createAction (text, new gui.Icon (iconFileName), isCheckable, isChecked);
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

gui.Action* Plugin.createAction (
	char const* text,
	gui.Icon* icon,
	bool isCheckable,
	bool isChecked
	)
{
	gui.Action* action = new gui.Action (text, icon);
	action.m_isCheckable = isCheckable;
	if (isCheckable)
		action.m_isChecked = isChecked;	
	
	m_pluginHost.m_menu.addAction (action);
	m_pluginHost.m_toolBar.addAction (action);
	return action;
}

//.............................................................................

class Session: Plugin
{
	char const* autoget property m_title;
	bool autoget property m_isTransmitEnabled;

	construct (PluginHost* host)
	{
		basetype.construct (host);
	}

	override size_t transmit (
		void const* p,
		size_t size
		) throws
	{
		return size;
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

class Layer: Plugin
{
	construct (PluginHost* host)
	{
		basetype.construct (host);
	}

	override size_t transmit (
		void const* p,
		size_t size
		) throws
	{
		return transmitNext (p, size);
	}

	size_t transmitNext (
		void const* p,
		size_t size
		);
}

//.............................................................................

} // namespace doc {

trace (char const* p);
