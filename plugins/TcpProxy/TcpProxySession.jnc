//..............................................................................
//
// TCP Proxy session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_Socket.jnc"
import "io_HostNameResolver.jnc"
import "ui_History.jnc"
import "ui_SocketUi.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"
import "std_List.jnc"
import "std_HashTable.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class TcpProxySession: doc.Session
{
protected:
	enum Defaults
	{
		LocalPort           = 8080,
		TxDisconnectTimeout = 1000, // wait TX on disconnect for at most 1 sec
	}

	enum ListenerState
	{
		Closed,
		Listening,
	}

	static char const* m_listenerStateStringTable[] =
	{
		"Not listening",
		"Listening",
	}

	enum ResolverState
	{
		Idle,
		Error,
		Resolving,
		Resolved,
	}

	enum ConnectionState
	{
		Closed,
		Resolving,
		Connecting,
		Connected,
	}

	enum ActionId
	{
		Listen,
		DisconnectAll,
		ApplyFilter,
		_Count,
	}

	enum IconId
	{
		Listen,
		StopListen,
		Disconnect,
		_Count,
	}

	enum StatusPaneId
	{
		ListenerState,
		TargetAddress,
		ClientCount,
		_Count,
	}

	class Client
	{
		std.ListEntry* m_entry;

		// client-to-proxy

		ConnectionState bindable m_clientState;
		io.Socket* m_clientSocket;
		io.SocketAddress m_clientAddress;
		uint_t m_clientSyncId;

		// proxy-to-server (target)

		ConnectionState bindable m_targetState;
		io.Socket m_targetSocket;
		io.SocketAddress m_targetAddress;
		std.Buffer m_targetPendingData;
		uint_t m_targetSyncId;
	}

protected:
	// server listener socket

	ListenerState bindable m_listenerState;
	io.Socket m_listenerSocket;
	io.SocketAddress m_localAddress;
	uint_t m_listenerSyncId;
	std.List m_clientList;
	size_t bindable m_clientCount;
	io.SocketAddress m_currentClientAddress;
	std.Buffer m_readBuffer;
	SocketClientLogFilter m_filter;

	// resolver

	ResolverState bindable m_resolverState;
	io.HostNameResolver m_resolver;
	uint_t m_resolverSyncId;
	std.HashTable m_resolverWaitSet;
	io.SocketAddress m_targetAddress;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_clientFilterCombo;
	ui.ComboBox* m_adapterCombo;
	ui.ComboBox* m_portCombo;
	ui.ComboBox* m_targetAddressCombo;

	// property grid

	ui.SocketPropertySet m_listenerSocketPropertySet;
	ui.SocketPropertySet m_connectionSocketPropertySet;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode listen();
	void stopListen();
	void disconnectAll();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

protected:
	onListen();
	onTargetAddressChanged();

	onTargetAddressEnter()
	{
		try resolve();
	}

	onPortComboEnter()
	{
		try listen();
	}

	onSetClientFilter()
	{
		try applyClientFilter(true);
	}

	onResolverEvent(
		uint_t syncId,
		io.HostNameResolverEvents triggeredEvents
		);

	onListenerSocketEvent(
		uint_t syncId,
		io.SocketEvents triggeredEvents
		);

	onClientSocketEvent(
		Client* client,
		uint_t syncId,
		io.SocketEvents triggeredEvents
		);

	onTargetSocketEvent(
		Client* client,
		uint_t syncId,
		io.SocketEvents triggeredEvents
		);

	bool errorcode applyClientFilter(bool rebuildLog);

	createUi();

	handleConnectError(
		Client* client,
		std.Error const* error
		);

	handleResolveError(std.Error const* error);
	waitListenerSocketEvents();
	waitClientSocketEvents(Client* client);
	waitTargetSocketEvents(Client* client);
	disconnect(Client* client);
	bool errorcode resolve();
	void cancelResolve();
	bool errorcode connectTarget(Client* client);
	addClient(Client* client);
	removeClient(Client* client);
	setCurrentClient(Client* client);
	size_t errorcode forwardDataFromClientToTarget(Client* client);
	size_t errorcode forwardDataFromTargetToClient(Client* client);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

TcpProxySession.construct(doc.PluginHost* pluginHost)
{
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
	pluginHost.m_log.addFilter(m_filter);
	m_stdSessionInfoSet.addLogListener(pluginHost.m_log);
}

TcpProxySession.createUi()
{
	// toolbar

	m_pluginHost.m_toolBar.addLabel("Filter:");
	m_clientFilterCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_clientFilterCombo.m_isEditable = true;
	m_clientFilterCombo.m_onEnter += onSetClientFilter;
	m_clientFilterCombo.m_placeholderText = "Enter client address to filter...";
	m_clientFilterCombo.m_toolTipText = "View only communications with the specified client";

	m_actionTable[ActionId.ApplyFilter] = createAction("Apply filter", "images/apply.png");
	m_actionTable[ActionId.ApplyFilter].m_onTriggered += onSetClientFilter;

	m_pluginHost.m_toolBar.addLabel("Target:");
	m_targetAddressCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_targetAddressCombo.m_isEditable = true;
	m_targetAddressCombo.m_editText = "127.0.0.1:1002";
	bindingof(m_targetAddressCombo.m_editText) += onTargetAddressChanged;
	m_targetAddressCombo.m_onEnter += onTargetAddressEnter;
	m_targetAddressCombo.m_placeholderText = "Enter a remote IP or hostname...";

	m_pluginHost.m_toolBar.addLabel("Adapter:");
	m_adapterCombo = ui.createNetworkAdapterComboBox(m_pluginHost.m_toolBar, ui.NetworkAdapterUsage.Listener);

	m_pluginHost.m_toolBar.addLabel("Port:");
	m_portCombo = m_pluginHost.m_toolBar.addComboBox();
	m_portCombo.m_isEditable = true;
	m_portCombo.m_onEnter += onPortComboEnter;

	m_iconTable[IconId.Listen] = new ui.Icon("images/listen.png");
	m_iconTable[IconId.StopListen] = new ui.Icon("images/pause.png");
	m_actionTable[ActionId.Listen] = createAction("Listen", m_iconTable [IconId.Listen]);
	m_actionTable[ActionId.Listen].m_onTriggered += onListen;
	m_actionTable[ActionId.DisconnectAll] = createAction("Disconnect All", "images/disconnect-all.png");
	m_actionTable[ActionId.DisconnectAll].m_onTriggered += disconnectAll;

	// status bar

	m_statusPaneTable[StatusPaneId.TargetAddress] = m_pluginHost.m_statusBar.addPane(, 80);
	m_statusPaneTable[StatusPaneId.ListenerState] = m_pluginHost.m_statusBar.addPane(, 80);
	m_statusPaneTable[StatusPaneId.ClientCount] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Server",
		"TCP server settings"
		);

	m_listenerSocketPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		group,,
		ui.SocketPropertySet.PropertyId.AllTcpServer &
		~ui.SocketPropertySet.PropertyId.TcpOptions
		);

	m_listenerSocketPropertySet.m_defaultLocalPort = Defaults.LocalPort;

	group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Connection",
		"TCP connection settings"
		);

	m_connectionSocketPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		group,,
		ui.SocketPropertySet.PropertyId.RemoteAddress |
		ui.SocketPropertySet.PropertyId.RemotePort |
		ui.SocketPropertySet.PropertyId.TcpOptions
		);

	m_bufferPropertySet.createPropertiesGrouped(m_pluginHost.m_propertyGrid);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("TCP proxy");

	m_stdSessionInfoSet.createInformationValues(m_pluginHost.m_informationGrid, infoGroup);
	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_propertyGrid.resizeNameColumnToContents();
	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

TcpProxySession.onListen()
{
	if (m_listenerState)
		stopListen();
	else
		try listen();
}

TcpProxySession.onTargetAddressChanged()
{
	cancelResolve();
	m_resolverState = ResolverState.Idle;
	m_targetAddressCombo.m_backgroundColor = ui.StdColor.White;
}

bool errorcode TcpProxySession.listen()
{
	stopListen();
	startSession();

	m_portCombo.addEditTextToHistory();

	io.SocketAddress address = *(io.SocketAddress const*)m_adapterCombo.m_currentData;
	address.m_port = (uint16_t)atoi(m_portCombo.m_editText);

	m_listenerSocket.open(io.Protocol.Tcp, &address);
	m_listenerSocket.listen();

	address = m_listenerSocket.m_address;
	m_logWriter.write(SocketLogRecordCode.Listening, &address, sizeof(address));
	m_localAddress = address;
	m_listenerState = ListenerState.Listening;
	waitListenerSocketEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;

catch:
	m_logWriter.writeLastError(SocketLogRecordCode.ListenError);
	m_listenerSocket.close();
	m_listenerState = ListenerState.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
	return false;
}

bool errorcode TcpProxySession.resolve()
{
	cancelResolve();

	m_targetAddressCombo.addEditTextToHistory();
	char const* addressString = m_targetAddressCombo.m_editText;

	io.SocketAddress address;
	bool isDirectAddress = try address.parse(addressString);
	if (isDirectAddress)
	{
		if (!address.m_port)
			address.m_port = m_connectionSocketPropertySet.m_remotePortProp.m_value;

		m_targetAddress = address;
		m_resolverState = ResolverState.Resolved;
		return true;
	}

	m_logWriter.write(SocketLogRecordCode.Resolving, addressString);
	m_resolver.resolve(addressString);

	m_resolver.wait(
		io.HostNameResolverEvents.Resolved | io.HostNameResolverEvents.Error,
		onResolverEvent~(++m_resolverSyncId) @ m_pluginHost.m_mainThreadScheduler
		);

	m_resolverState = ResolverState.Resolving;
	return true;

catch:
	handleResolveError(std.getLastError());
	return false;
}

void TcpProxySession.cancelResolve()
{
	if (m_resolverState != ResolverState.Resolving)
		return;

	m_logWriter.write(SocketLogRecordCode.ResolveCancelled);

	std.MapEntry const* entry = m_resolverWaitSet.m_head;
	while (entry)
	{
		Client* client = entry.m_value;
		entry = entry.m_next;
		disconnect(client);
	}

	m_resolver.cancel();
	m_resolverSyncId++;
	m_resolverState = ResolverState.Idle;
}

TcpProxySession.stopListen()
{
	if (!m_listenerState)
		return;

	m_logWriter.write(SocketLogRecordCode.ListenStopped, &m_localAddress, sizeof(m_localAddress));
	m_listenerSocket.close();
	m_listenerState = ListenerState.Closed;
	m_listenerSyncId++;
	m_stdSessionInfoSet.endSession();
}

TcpProxySession.disconnectAll()
{
	while (m_clientList.m_count)
		disconnect(m_clientList.m_head.m_data);
}

TcpProxySession.disconnect(TcpProxySession.Client* client)
{
	TcpDisconnectLogRecordParams params;
	params.m_address = client.m_clientAddress;
	params.m_flags = 0;

	if (m_connectionSocketPropertySet.m_tcpResetProp.m_value)
		params.m_flags |= TcpDisconnectLogRecordFlags.Reset;

	m_logWriter.write(SocketLogRecordCode.ClientDisconnected, &params, sizeof(params));
	removeClient(client);
}

void TcpProxySession.addClient(TcpProxySession.Client* client)
{
	client.m_entry = m_clientList.add(client);
	m_clientCount = m_clientList.m_count;
	m_currentClientAddress = client.m_clientAddress;
}

void TcpProxySession.removeClient(TcpProxySession.Client* client)
{
	client.m_clientState = ConnectionState.Closed;
	client.m_clientSocket.close();
	client.m_clientSyncId++;
	client.m_targetState = ConnectionState.Closed;
	client.m_targetSocket.close();
	client.m_targetSyncId++;

	m_clientList.remove(client.m_entry);
	m_clientCount = m_clientList.m_count;
	m_currentClientAddress = null;
}

void TcpProxySession.setCurrentClient(TcpProxySession.Client* client)
{
	if (m_currentClientAddress.isEqual(client.m_clientAddress))
		return;

	m_logWriter.write(
		SocketLogRecordCode.ClientChanged,
		&client.m_clientAddress,
		sizeof(client.m_clientAddress)
		);

	m_currentClientAddress = client.m_clientAddress;
}

bool errorcode TcpProxySession.applyClientFilter(bool rebuildLog)
{
	m_pluginHost.m_log.suspend();
	bool result = try m_filter.setFilter(m_clientFilterCombo.m_editText);
	m_pluginHost.m_log.resume();

	if (!result)
	{
		m_clientFilterCombo.m_backgroundColor = ui.StdColor.PastelRed;
		return false;
	}

	m_clientFilterCombo.addEditTextToHistory();
	m_clientFilterCombo.m_backgroundColor = ui.ColorFlags.Transparent;

	if (rebuildLog)
		m_pluginHost.m_log.rebuild();

	return true;
}

bool errorcode TcpProxySession.connectTarget(TcpProxySession.Client* client)
{
	if (m_resolverState == ResolverState.Idle)
		resolve(); // and then inspect resolver state again

	switch (m_resolverState)
	{
	case ResolverState.Resolved:
		break;

	case ResolverState.Resolving:
		m_resolverWaitSet.add(client, null);
		return true;

	default:
		assert(false);
	}

	m_logWriter.write(SocketLogRecordCode.Connecting, &m_targetAddress, sizeof(m_targetAddress));
	client.m_targetSocket.open(m_targetAddress.m_family, io.Protocol.Tcp);
	client.m_targetSocket.connect(&m_targetAddress);
	client.m_targetState = ConnectionState.Connecting;
	waitTargetSocketEvents(client);
	return true;
}

void TcpProxySession.load(doc.Storage* storage)
{
	m_portCombo.loadHistory(storage, "portHistory");
	m_targetAddressCombo.loadHistory(storage, "targetAddressHistory");
	m_adapterCombo.m_currentIndex  = storage.readInt("adapterIdx");
	m_portCombo.m_editText = storage.readString("port");
	m_targetAddressCombo.m_editText = storage.readString("targetAddress");

	m_listenerSocketPropertySet.load(storage);
	m_connectionSocketPropertySet.load(storage);
	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void TcpProxySession.save(doc.Storage* storage)
{
	m_portCombo.saveHistory(storage, "portHistory");
	m_targetAddressCombo.saveHistory(storage, "targetAddressHistory");

	m_listenerSocketPropertySet.save(storage);
	m_connectionSocketPropertySet.save(storage);
	m_bufferPropertySet.save(storage);

	storage.writeInt("adapterIdx", (int) m_adapterCombo.m_currentIndex);
	storage.writeString("port", m_portCombo.m_editText);
	storage.writeString("targetAddress", m_targetAddressCombo.m_editText);
}

void TcpProxySession.updateProperties()
{
	m_listenerSocketPropertySet.m_adapterProp.m_currentIndex = m_adapterCombo.m_currentIndex;
	m_listenerSocketPropertySet.m_localPortProp.m_value = atoi(m_portCombo.m_editText);
	m_connectionSocketPropertySet.m_remoteAddressProp.m_value = m_targetAddressCombo.m_editText;
}

bool errorcode TcpProxySession.applyProperties()
{
	m_adapterCombo.m_currentIndex = m_listenerSocketPropertySet.m_adapterProp.m_currentIndex;
	m_portCombo.m_editText = $"$(m_listenerSocketPropertySet.m_localPortProp.m_value)";
	m_targetAddressCombo.m_editText = m_connectionSocketPropertySet.m_remoteAddressProp.m_value;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
	return true;
}

void TcpProxySession.restoreDefaultProperties()
{
	m_listenerSocketPropertySet.restoreDefaults();
	m_connectionSocketPropertySet.restoreDefaults();
	m_bufferPropertySet.restoreDefaults();
}

TcpProxySession.onListenerSocketEvent(
	uint_t syncId,
	io.SocketEvents triggeredEvents
	)
{
	if (syncId != m_listenerSyncId)
		return;

	if (triggeredEvents & io.SocketEvents.IoError)
	{
		m_logWriter.write(log.StdRecordCode.Error, m_listenerSocket.m_ioError);
		m_listenerSocket.close();
		m_listenerState = ListenerState.Closed;
		m_stdSessionInfoSet.endSession();
		return;
	}

	if (triggeredEvents & io.SocketEvents.IncomingConnection)
	{
		io.SocketAddress address;
		io.Socket* socket = m_listenerSocket.accept(&address, true);
		m_logWriter.write(SocketLogRecordCode.ClientConnected, &address, sizeof(address));

		socket.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
		socket.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
		socket.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
		socket.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
		socket.m_options = ui.getSocketOptions(m_connectionSocketPropertySet, m_bufferPropertySet);
		socket.unsuspend();

		Client* client = new Client;
		client.m_clientState = ConnectionState.Connected;
		client.m_clientSocket = socket;
		client.m_clientAddress = address;

		addClient(client);
		waitClientSocketEvents(client);

		bool result = try connectTarget(client);
		if (!result)
		{
			handleConnectError(client, std.getLastError());
			removeClient(client);
		}
	}

	waitListenerSocketEvents();
}

TcpProxySession.onClientSocketEvent(
	TcpProxySession.Client* client,
	uint_t syncId,
	io.SocketEvents triggeredEvents
	)
{
	if (syncId != client.m_clientSyncId)
		return;

	if (triggeredEvents & io.SocketEvents.IoError)
	{
		setCurrentClient(client);
		m_logWriter.write(log.StdRecordCode.Error, client.m_clientSocket.m_ioError);
		disconnect(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.Disconnected)
	{
		for (;;) // read all the buffered data
		{
			size_t result = forwardDataFromTargetToClient(client);
			if (!result)
				break;
		}

		if (client.m_targetState == ConnectionState.Connected)
			client.m_targetSocket.blockingWait(io.SocketEvents.WriteBufferEmpty, Defaults.TxDisconnectTimeout);

		TcpDisconnectLogRecordParams params;
		params.m_address = client.m_clientAddress;
		params.m_flags = TcpDisconnectLogRecordFlags.Remote;

		if (triggeredEvents & io.SocketEvents.Reset)
			params.m_flags |= TcpDisconnectLogRecordFlags.Reset;

		m_logWriter.write(SocketLogRecordCode.Disconnected, &params, sizeof(params));
		disconnect(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.IncomingData)
		forwardDataFromClientToTarget(client);

	waitClientSocketEvents(client);
}

TcpProxySession.onTargetSocketEvent(
	TcpProxySession.Client* client,
	uint_t syncId,
	io.SocketEvents triggeredEvents
	)
{
	if (syncId != client.m_targetSyncId)
		return;

	if (triggeredEvents & io.SocketEvents.IoError)
	{
		handleConnectError(client, client.m_targetSocket.m_ioError);
		disconnect(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.Connected)
	{
		m_logWriter.write(SocketLogRecordCode.ConnectCompleted);
		client.m_targetState = ConnectionState.Connected;

		if (client.m_targetPendingData.m_size)
		{
			m_logWriter.write(log.StdRecordCode.Tx, client.m_targetPendingData.m_p, client.m_targetPendingData.m_size);
			client.m_targetSocket.write(client.m_targetPendingData.m_p, client.m_targetPendingData.m_size);
			client.m_targetPendingData.clear();
		}

		if (client.m_clientState != ConnectionState.Connected)
		{
			client.m_targetSocket.blockingWait(io.SocketEvents.WriteBufferEmpty, Defaults.TxDisconnectTimeout);
			disconnect(client);
			return;
		}
	}

	if (triggeredEvents & io.SocketEvents.Disconnected)
	{
		for (;;) // read all the buffered data
		{
			size_t result = forwardDataFromClientToTarget(client);
			if (!result)
				break;
		}

		if (client.m_clientState == ConnectionState.Connected)
			client.m_clientSocket.blockingWait(io.SocketEvents.WriteBufferEmpty, Defaults.TxDisconnectTimeout);

		TcpDisconnectLogRecordParams params;
		params.m_address = m_targetAddress;
		params.m_flags = TcpDisconnectLogRecordFlags.Remote;

		if (triggeredEvents & io.SocketEvents.Reset)
			params.m_flags |= TcpDisconnectLogRecordFlags.Reset;

		m_logWriter.write(SocketLogRecordCode.Disconnected, &params, sizeof(params));
		disconnect(client);
		return;
	}

	if (triggeredEvents & io.SocketEvents.IncomingData)
		forwardDataFromTargetToClient(client);

	waitTargetSocketEvents(client);
}

TcpProxySession.onResolverEvent(
	uint_t syncId,
	io.HostNameResolverEvents triggeredEvents
	)
{
	if (syncId != m_resolverSyncId)
		return;

	if (triggeredEvents & io.HostNameResolverEvents.Error)
	{
		handleResolveError(m_resolver.m_error);
		return;
	}

	m_logWriter.write(
		SocketLogRecordCode.ResolveCompleted,
		m_resolver.m_addressTable,
		sizeof(io.SocketAddress)
		);

	m_targetAddress = m_resolver.m_addressTable[0];
	if (!m_targetAddress.m_port)
		m_targetAddress.m_port = m_connectionSocketPropertySet.m_remotePortProp.m_value;

	m_resolverState = ResolverState.Resolved;
}

size_t errorcode TcpProxySession.forwardDataFromTargetToClient(TcpProxySession.Client* client)
{
	size_t result = client.m_targetSocket.read(m_readBuffer.m_p, m_readBuffer.m_size);
	if (!result)
		return 0;

	setCurrentClient(client);
	m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);

	if (client.m_clientState != ConnectionState.Connected)
		client.m_clientSocket.write(m_readBuffer.m_p, result);

	return result;
}

size_t errorcode TcpProxySession.forwardDataFromClientToTarget(TcpProxySession.Client* client)
{
	printf("forwardDataFromClientToTarget\n");

	size_t result = client.m_clientSocket.read(m_readBuffer.m_p, m_readBuffer.m_size);
	if (!result)
		return 0;

	if (client.m_targetState != ConnectionState.Connected)
	{
		printf("   appending %d bytes\n", result);
		client.m_targetPendingData.append(m_readBuffer.m_p, result);
	}
	else
	{
		printf("   transmitting %d bytes\n", result);

		setCurrentClient(client);
		m_logWriter.write(log.StdRecordCode.Tx, m_readBuffer.m_p, result);
		client.m_targetSocket.write(m_readBuffer.m_p, result);
	}

	return result;
}

TcpProxySession.handleResolveError(std.Error const* error)
{
	m_logWriter.write(SocketLogRecordCode.ResolveError, error);
	m_resolver.cancel();
	m_resolverState = ResolverState.Error;
}

TcpProxySession.handleConnectError(
	TcpProxySession.Client* client,
	std.Error const* error
	)
{
	m_logWriter.write(SocketLogRecordCode.ConnectError, error);
	client.m_targetSocket.close();
	client.m_targetState = ConnectionState.Closed;
}

TcpProxySession.waitListenerSocketEvents()
{
	m_listenerSocket.wait(
		io.SocketEvents.IoError |
		io.SocketEvents.IncomingConnection,
		onListenerSocketEvent~(++m_listenerSyncId) @ m_pluginHost.m_mainThreadScheduler
		);
}

TcpProxySession.waitClientSocketEvents(TcpProxySession.Client* client)
{
	client.m_clientSocket.wait(
		io.SocketEvents.IoError |
		io.SocketEvents.IncomingData |
		io.SocketEvents.Disconnected |
		io.SocketEvents.Reset,
		onClientSocketEvent~(client, ++client.m_clientSyncId) @ m_pluginHost.m_mainThreadScheduler
		);
}

TcpProxySession.waitTargetSocketEvents(TcpProxySession.Client* client)
{
	io.SocketEvents eventMask = io.SocketEvents.IoError;

	if (client.m_targetState != ConnectionState.Connected)
		eventMask |= io.SocketEvents.Connected;
	else
		eventMask |=
			io.SocketEvents.IncomingData |
			io.SocketEvents.Disconnected |
			io.SocketEvents.Reset;

	client.m_targetSocket.wait(
		eventMask,
		onTargetSocketEvent~(client, ++client.m_targetSyncId) @ m_pluginHost.m_mainThreadScheduler
		);
}

reactor TcpProxySession.m_uiReactor
{
	m_title = $"TCP @ $(m_portCombo.m_editText) -> $(m_targetAddressCombo.m_editText)";
	m_actionTable[ActionId.Listen].m_text = m_listenerState ? "Stop Listening" : "Listen";
	m_actionTable[ActionId.Listen].m_icon = m_iconTable[m_listenerState ? IconId.StopListen : IconId.Listen];
	m_actionTable[ActionId.DisconnectAll].m_isEnabled = m_clientCount;
	m_statusPaneTable[StatusPaneId.ListenerState].m_text = m_listenerStateStringTable[m_listenerState];
	m_statusPaneTable[StatusPaneId.ClientCount].m_text = $"$m_clientCount client(s)";

	m_statusPaneTable[StatusPaneId.TargetAddress].m_text =
		m_resolverState == ResolverState.Resolving ? "Resolving..." :
		m_resolverState == ResolverState.Error ? "Resolve error" :
		m_resolverState == ResolverState.Resolved ? $"Target: %1"(m_targetAddress.getString()) :
		null;

	m_targetAddressCombo.m_backgroundColor = m_resolverState == ResolverState.Error ?
		ui.StdColor.PastelRed :
		ui.StdColor.White;
}

//..............................................................................
