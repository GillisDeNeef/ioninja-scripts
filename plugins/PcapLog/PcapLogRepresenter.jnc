//..............................................................................
//
// Pcap log representer
//

import "log_Representation.jnc"
import "PcapLogRecordCode.jnc"

//..............................................................................

bool representPcapLog(
	log.Representation* representation,
	uint64_t recordCode,
	void const* p,
	size_t size,
	uint_t foldFlags
	)
{
	printf("representPcapLog (%llu)\n", recordCode);

	switch (recordCode)
	{
	case PcapLogRecordCode.StartingCapture:
		char const* filter = (char const*) p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;

		representation.addHyperText(
			PcapLogPartCode.StartingCapture,
			*filter ?
				$"Starting capture with filter '\e[34m$filter\e[m'..." :
				"Starting capture without filter..."
			);

		break;

	case PcapLogRecordCode.CaptureStarted:
		representation.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Listen;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Connect;
		representation.addPlainText(PcapLogPartCode.CaptureCompleted, "ok");
		break;

	case PcapLogRecordCode.CaptureError:
		std.Error const* error = (std.Error const*) p;
		representation.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addPlainText(PcapLogPartCode.CaptureCompleted, $"error: $(error.m_description)");
		break;

	case PcapLogRecordCode.CaptureStopped:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.StopListen;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Disconnect;
		representation.addPlainText("Capture stopped");
		break;

	case PcapLogRecordCode.ChangingFilter:
		char const* filter = (char const*) p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText(PcapLogPartCode.ChangingFilter, $"Changing capture filter to \e[34m$filter\e[m...");
		break;

	case PcapLogRecordCode.FilterChanged:
		representation.addPlainText(PcapLogPartCode.FilterCompleted, "ok");
		break;

	case PcapLogRecordCode.FilterError:
		std.Error const* error = (std.Error const*) p;
		representation.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addPlainText(PcapLogPartCode.FilterCompleted, $"error: $(error.m_description)");
		break;

	default:
		return false;
	}

	return true;
}

//..............................................................................
