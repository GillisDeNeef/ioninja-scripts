//..............................................................................
//
// ModBus RTU Protocol Analyzer layer
//

import "io_ModBus.jnc"
import "doc_Plugin.jnc"
import "std_Buffer.jnc"
import "crc16.jnc"

//..............................................................................

class ModBusRtuLayer:
	doc.Layer,
	log.Converter
{
	// checking for correct modbus delays (1.5/3.5 chars) is not possible
	// due to  insufficient precision of timestamps	(we get timestamp of read
	// completion and no timestamps of individual bytes)

	enum: uint64_t
	{
		MinInterFrameDelay = 500 * 10000, // 500 ms
	}

protected:
	uint_t m_lastRecordCode;
	uint64_t m_lastRecordTimestamp;
	std.Buffer m_dataBuffer;

public:
	construct (doc.PluginHost* pluginHost);

	override bool convert (
		log.Writer* writer,
		uint64_t timestamp,
		uint_t recordCode,
		void const* p,
		size_t size
		);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

ModBusRtuLayer.construct (doc.PluginHost* pluginHost)
{
	basetype1.construct (pluginHost);

	m_lastRecordCode = 0;

	pluginHost.m_log.attachConverter (this, representModBusRtuLog);
}

bool ModBusRtuLayer.convert (
	log.Writer* writer,
	uint64_t timestamp,
	uint_t recordCode,
	void const* p,
	size_t size
	)
{
	bool result;
	io.ModBusRole role;
	ModBusRtuLogRecordCode packetRecordeCode;

	switch (recordCode)
	{
	case log.StdRecordCode.Tx:
		role = io.ModBusRole.Master;
		packetRecordeCode = ModBusRtuLogRecordCode.MasterPacket;
		break;

	case log.StdRecordCode.Rx:
		role = io.ModBusRole.Slave;
		packetRecordeCode = ModBusRtuLogRecordCode.SlavePacket;
		break;

	default:
		return false;
	}

	bool isNewFrame = false;

	if (recordCode != m_lastRecordCode)
	{
		isNewFrame = true;
	}
	else if (timestamp - m_lastRecordTimestamp >= MinInterFrameDelay)
	{
		isNewFrame = true;
		writer.write (log.StdRecordCode.Break);
	}

	if (isNewFrame) // drop the previous data (if any)
		m_dataBuffer.clear ();

	m_lastRecordCode = 0;
	m_dataBuffer.append (p, size);

	io.ModBusRtuAduHdr const* aduHdr = (io.ModBusRtuAduHdr const*) m_dataBuffer.m_p;
	size_t aduSize = io.getModBusRtuAduSize (role, aduHdr, m_dataBuffer.m_size);
	if (aduSize == -1)
	{
		uint8_t code = *(uint8_t const*) (aduHdr + 1);
		writer.write (timestamp, ModBusRtuLogRecordCode.InvalidFunction, &code, sizeof (code));
		return false; // unknown or unsupported function
	}

	if (m_dataBuffer.m_size < aduSize) // wait for the next chunk of data
	{
		m_lastRecordCode = recordCode;
		m_lastRecordTimestamp = timestamp;
		return false;
	}

	writer.write (timestamp, recordCode, p, size); // actual data first
	writer.write (timestamp, packetRecordeCode, aduHdr, aduSize);

	if (m_dataBuffer.m_size > aduSize)
		writer.write (
			timestamp,
			recordCode,
			m_dataBuffer.m_p + aduSize,
			m_dataBuffer.m_size - aduSize
			);

	return true;
}

//..............................................................................
