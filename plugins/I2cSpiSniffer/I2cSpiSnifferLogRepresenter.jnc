//..............................................................................
//
// I2C sniffer log representer
//

import "log_Representation.jnc"

//..............................................................................

bool representI2cSpiSnifferLog(
	log.Representation* representation,
	uint64_t recordCode,
	void const* p,
	size_t size,
	uint_t foldFlags
	)
{
	switch (recordCode)
	{
	case I2cSpiSnifferLogRecordCode.DeviceOpened:
		char const* name = (char const*) p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"Opened tap device \e[34m$name\e[m");
		break;

	case I2cSpiSnifferLogRecordCode.InterfaceClaimed:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"Interface \e[34m#0\e[m claimed");
		break;

	case I2cSpiSnifferLogRecordCode.InEndpointOpened:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"In endpoint \e[34m#86\e[m opened");
		break;

	case I2cSpiSnifferLogRecordCode.OutEndpointOpened:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"Out endpoint \e[34m#2\e[m opened");
		break;

	case I2cSpiSnifferLogRecordCode.CaptureStarted:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Connect;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Connect;
		representation.addHyperText($"Capture started");
		break;

	case I2cSpiSnifferLogRecordCode.CaptureStopped:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Disconnect;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Disconnect;
		representation.addHyperText("Capture stopped");
		break;

	case I2cSpiSnifferLogRecordCode.CaptureError:
		std.Error const* error = (std.Error const*)p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText($"Cannot open sniffer: $(error.m_description)");
		break;

	case I2cSpiSnifferLogRecordCode.UnexpectedCode:
		uint_t code = ((uint_t const*)p)[0];
		uint_t state = ((uint_t const*)p)[1];
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Error;
		representation.addHyperText($"Unexpected code from sniffer (code: $code, decoder state: $state)");
		break;

	case I2cSpiSnifferLogRecordCode.IncompleteByte:
		uint_t bitCount = *(uint_t const*) p;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText($"Incomplete byte received ($(bitCount) bit(s))");
		break;

	case I2cSpiSnifferLogRecordCode.FpgaBufferOverflow:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("Hardware buffer overflow");
		break;

	case I2cSpiSnifferLogRecordCode.CypressBufferCleared:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText($"Hardware buffer cleared");
		break;

	case I2cSpiSnifferLogRecordCode.StillWaiting:
		representation.m_lineAttr.m_backColor = ui.StdColor.PastelGray;
		representation.addHyperText("...still waiting...");
		break;

	case I2cSpiSnifferLogRecordCode.FpgaDisabled:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("FPGA disabled");
		break;

	case I2cSpiSnifferLogRecordCode.FirmwareUpToDate:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("FPGA firmware is up-to-date");
		break;

	case I2cSpiSnifferLogRecordCode.FirmwareUpdateRequired:
		uint_t version = ((RpdVersion const*) p)->m_firmwareVersion;
		char const* fileName = (char const*) ((RpdVersion const*) p + 1);

		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText(
			$"FPGA firmware update required: file \e[34m%1\e[m version \e[34m%2.%3\e[m" (
			fileName,
			(version >> 8) & 0xff,
			version & 0xff
			));
		break;

	case I2cSpiSnifferLogRecordCode.ErasingFlash:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("Erasing flash memory...");
		break;

	case I2cSpiSnifferLogRecordCode.UploadingFirmware:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("Uploading FPGA firmware...");
		break;

	case I2cSpiSnifferLogRecordCode.EnablingFpga:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("Enabling FPGA...");
		break;

	case I2cSpiSnifferLogRecordCode.FpgaReady:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("FPGA ready");
		break;

	case I2cSpiSnifferLogRecordCode.FirmwareUploadCancelled:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("Uploading FPGA firmware cancelled");
		break;

	case I2cSpiSnifferLogRecordCode.FirmwareUploadCompleted:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("Uploading FPGA firmware completed");
		break;

	case I2cSpiSnifferLogRecordCode.I2cStart:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText(I2cSpiSnifferLogPartCode.I2cStartBegin, "I2C start");
		break;

	case I2cSpiSnifferLogRecordCode.I2cFinalizeStart7Bit:
		uint16_t hdr = *(const uint16_t*) p;
		bool isRead = hdr & 0x01;
		bool isAck = hdr & 0x0100;

		representation.addHyperText(
			I2cSpiSnifferLogPartCode.I2cStartEnd,
			$"\x15I2C start (%1, I2C 7-bit address: \e[%2m0x%(3; 02x)\e[m)" (
				isRead ? "read" : "write",
				isAck ? "34" : "34;9",
				(hdr & 0xfe) >> 1
				)
			);

		representation.m_lineAttr.m_iconIdx = isRead ? log.StdLogIcon.Rx : log.StdLogIcon.Tx;
		representation.m_lineAttr.m_backColor = ui.ColorFlags.Transparent;
		representation.m_lineAttr.m_textColor = ui.StdColor.Red;
		representation.m_lineAttr.m_fontFlags = isAck ? 0 : ui.FontFlags.Strikeout;
		representation.addBin(isRead ? log.StdRecordCode.Rx : log.StdRecordCode.Tx, p, 1);
		break;

	case I2cSpiSnifferLogRecordCode.I2cFinalizeStart10Bit:
		uint32_t hdr = *(const uint32_t*) p;
		bool isRead = hdr & 0x01;
		bool isAck1 = hdr & 0x00010000;
		bool isAck2 = hdr & 0x00020000;

		representation.addHyperText(
			I2cSpiSnifferLogPartCode.I2cStartEnd,
			$"\x15I2C start (%1, I2C 10-bit address: \e[%2m0x%(3; 03x)\e[m)" (
				isRead ? "read" : "write",
				isAck1 && isAck2 ? "34" : "34;9",
				((hdr & 0x06) << 7) | ((hdr & 0xff00) >> 8)
				)
			);

		representation.m_lineAttr.m_iconIdx = isRead ? log.StdLogIcon.Rx : log.StdLogIcon.Tx;
		representation.m_lineAttr.m_backColor = ui.ColorFlags.Transparent;
		representation.m_lineAttr.m_textColor = ui.StdColor.Red;

		uint64_t binRecordCode = isRead ? log.StdRecordCode.Rx : log.StdRecordCode.Tx;
		ui.FontFlags fontFlags1 = isAck1 ? 0 : ui.FontFlags.Strikeout;
		ui.FontFlags fontFlags2 = isAck2 ? 0 : ui.FontFlags.Strikeout;

		if (fontFlags1 == fontFlags2)
		{
			representation.m_lineAttr.m_fontFlags = fontFlags1;
			representation.addBin(binRecordCode, p, 2);
		}
		else
		{
			representation.m_lineAttr.m_fontFlags = fontFlags1;
			representation.addBin(binRecordCode, p, 1);
			representation.m_lineAttr.m_fontFlags = fontFlags2;
			representation.addBin(binRecordCode, p + 1, 1);
		}

		break;

	case I2cSpiSnifferLogRecordCode.I2cStop:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Stop;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("I2C stop");
		break;

	case I2cSpiSnifferLogRecordCode.I2cStopOutOfTransaction:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		representation.addHyperText("I2C \e[31mstop\e[m out-of-transaction");
		break;

	case I2cSpiSnifferLogRecordCode.I2cTxNack:
		representation.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Tx;
		representation.m_lineAttr.m_textColor = log.StdLogColor.Tx;
		representation.m_lineAttr.m_fontFlags = ui.FontFlags.Strikeout;
		representation.addBin(log.StdRecordCode.Tx, p, size);
		break;

	case I2cSpiSnifferLogRecordCode.I2cRxNack:
		representation.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Rx;
		representation.m_lineAttr.m_textColor = log.StdLogColor.Rx;
		representation.m_lineAttr.m_fontFlags = ui.FontFlags.Strikeout;
		representation.addBin(log.StdRecordCode.Rx, p, size);
		break;

	case I2cSpiSnifferLogRecordCode.SpiStart:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("SPI SS \e[32mlow\e[m");
		break;

	case I2cSpiSnifferLogRecordCode.SpiStop:
		representation.m_lineAttr.m_iconIdx = log.StdLogIcon.Stop;
		representation.m_lineAttr.m_backColor = log.StdLogColor.Info;
		representation.addHyperText("SPI SS \e[31mhigh\e[m");
		break;

	default:
		return false;
	}

	return true;
}

//..............................................................................
