//..............................................................................
//
// Process session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_ChildProcess.jnc"
import "ui_History.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class ProcessSession: doc.Session
{
protected:
	enum State
	{
		Idle,
		Running,
	}

	enum Access
	{
		ReadWrite,
		ReadOnly,
		WriteOnly,
	}

	enum ActionId
	{
		Start,
		Terminate,
		_Count,
	}

	enum IconId
	{
		Start,
		Detach,
		Terminate,
		_Count,
	}

	enum StatusPaneId
	{
		State,
		_Count,
	}

protected:
	// file

	State bindable m_state;
	io.ChildProcess m_process;
	std.Buffer m_readBuffer;
	uint_t m_syncId;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_commandLineCombo;

	// property grid

	ui.StringProperty* m_commandLineProp;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode start();
	close();
	terminate();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
		);

protected:
	onStart()
	{
		m_state ? close() : try start();
	}

	onProcessNameComboEnter()
	{
		try start();
	}

	onChildProcessEvent(
		uint_t syncId,
		io.ChildProcessEvents triggeredEvents
		);

	io.ChildProcessOptions getChildProcessOptions();
	waitChildProcessEvents();
	createUi();
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

ProcessSession.construct(doc.PluginHost* pluginHost)
{
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
	m_stdSessionInfoSet.addLogListener(pluginHost.m_log);
}

ProcessSession.createUi()
{
	static ui.EnumPropertyOption accessModeTable[] =
	{
		{ "Read/Write",  Access.ReadWrite },
		{ "Read-only",   Access.ReadOnly  },
		{ "Write-only",  Access.WriteOnly },
	}

	// toolbar

	m_pluginHost.m_toolBar.addLabel("Command line:");
	m_commandLineCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_commandLineCombo.m_isEditable = true;
	m_commandLineCombo.m_onEnter += onProcessNameComboEnter;
	m_commandLineCombo.m_placeholderText = "Enter a command line...";

	m_iconTable[IconId.Start] = new ui.Icon("images/start.png");
	m_iconTable[IconId.Detach] = new ui.Icon("images/detach.png");
	m_iconTable[IconId.Terminate] = new ui.Icon("images/terminate.png");

	m_actionTable[ActionId.Start] = createAction("Open", m_iconTable[IconId.Start]);
	m_actionTable[ActionId.Start].m_onTriggered += onStart;

	m_actionTable[ActionId.Terminate] = createAction("Terminate", m_iconTable[IconId.Terminate]);
	m_actionTable[ActionId.Terminate].m_onTriggered += terminate;
	m_actionTable[ActionId.Terminate].m_isEnabled = false;

	// status bar

	m_statusPaneTable[StatusPaneId.State] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Connection",
		"Process stream-specific connection settings"
		);

	m_commandLineProp = m_pluginHost.m_propertyGrid.createStringProperty(
		group,,
		"Process name",
		"Enter a file name...",
		"Specify a file name to open"
		);

	m_bufferPropertySet.createPropertiesGrouped(m_pluginHost.m_propertyGrid);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("Process session");

	m_stdSessionInfoSet.createInformationValues(m_pluginHost.m_informationGrid, infoGroup);
	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

bool errorcode ProcessSession.start()
{
	close();
	startSession();

	m_commandLineCombo.addEditTextToHistory();

	char const* commandLine = m_commandLineCombo.m_editText;

	m_process.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
	m_process.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
	m_process.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
	m_process.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
	m_process.m_options = getChildProcessOptions();
	m_process.start(commandLine);

	m_logWriter.write(ProcessLogRecordCode.ProcessStarted, commandLine);
	m_state = State.Running;

	waitChildProcessEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;

catch:
	std.Error const* error = std.getLastError();
	m_logWriter.write(
		ProcessLogRecordCode.ProcessStartError,
		error,
		error.m_size
		);

	m_process.close();
	return false;
}

ProcessSession.close()
{
	if (m_state != State.Running)
		return;

	m_logWriter.write(ProcessLogRecordCode.Detached);
	m_process.close();
	m_state = State.Idle;
	m_syncId++;
	m_stdSessionInfoSet.endSession();
}

ProcessSession.terminate()
{
	if (m_state != State.Running)
		return;

	try m_process.terminate();
	m_logWriter.write(ProcessLogRecordCode.ProcessTerminated);
}

io.ChildProcessOptions ProcessSession.getChildProcessOptions()
{
	io.ChildProcessOptions options;

	if (m_bufferPropertySet.m_keepReadBlockSizeProp.m_value)
		options |= io.ChildProcessOptions.KeepReadBlockSize;

	if (m_bufferPropertySet.m_keepWriteBlockSizeProp.m_value)
		options |= io.ChildProcessOptions.KeepWriteBlockSize;

	return options;
}

ProcessSession.waitChildProcessEvents()
{
	io.ChildProcessEvents eventMask =
		io.ChildProcessEvents.IoError |
		io.ChildProcessEvents.ReadBufferFull |
		io.ChildProcessEvents.IncomingData |
		io.ChildProcessEvents.ProcessFinished |
		io.ChildProcessEvents.ProcessCrashed;

	if (!m_isTransmitEnabled)
		eventMask |= io.ChildProcessEvents.WriteBufferReady;

	m_process.wait(eventMask, onChildProcessEvent~(++m_syncId) @ m_pluginHost.m_mainThreadScheduler);
}

void ProcessSession.load(doc.Storage* storage)
{
	m_commandLineCombo.loadHistory(storage, "commandLineHistory");
	m_commandLineCombo.m_editText = storage.readString("commandLine");

	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void ProcessSession.save(doc.Storage* storage)
{
	m_bufferPropertySet.save(storage);

	m_commandLineCombo.saveHistory(storage, "commandLineHistory");
	storage.writeString("commandLine", m_commandLineCombo.m_editText);
}

void ProcessSession.updateProperties()
{
	m_commandLineProp.m_value = m_commandLineCombo.m_editText;
}

bool errorcode ProcessSession.applyProperties()
{
	m_commandLineCombo.m_editText = m_commandLineProp.m_value;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);

	if (!m_process.m_isOpen)
		return true;

	m_process.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
	m_process.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
	m_process.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
	m_process.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
	m_process.m_options = getChildProcessOptions();
	return true;
}

void ProcessSession.restoreDefaultProperties()
{
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode ProcessSession.transmit(
	void const* p,
	size_t size
	)
{
	if (m_state != State.Running)
		return -1;

	size_t result = m_process.write(p, size);
	if (result)
		m_logWriter.write(log.StdRecordCode.Tx, p, result);

	return result;
}

ProcessSession.onChildProcessEvent(
	uint_t syncId,
	io.ChildProcessEvents triggeredEvents
	)
{
	if (syncId != m_syncId)
		return;

	if (triggeredEvents & io.ChildProcessEvents.IoError)
	{
		m_logWriter.write(log.StdRecordCode.Error, m_process.m_ioError, m_process.m_ioError.m_size);
		close();
		return;
	}

	if ((triggeredEvents & io.ChildProcessEvents.ReadBufferFull) &&
		m_bufferPropertySet.m_notifyReadBufferFullProp.m_value)
		m_logWriter.write(log.StdRecordCode.RxBufferFull);

	if (triggeredEvents & io.ChildProcessEvents.IncomingData)
	{
		size_t result = m_process.read(m_readBuffer.m_p, m_readBuffer.m_size);
		if (result)
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
	}

	if (triggeredEvents & io.ChildProcessEvents.ProcessFinished)
	{
		for (;;) // read all the buffered data
		{
			size_t result = m_process.read(m_readBuffer.m_p, m_readBuffer.m_size);
			if (!result)
				break;

			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
		}

		m_logWriter.write(ProcessLogRecordCode.ProcessFinished);
		close();
		return;
	}

	if (io.ChildProcessEvents.WriteBufferReady)
		m_isTransmitEnabled = true;

	waitChildProcessEvents();
}

reactor ProcessSession.m_uiReactor
{
	m_title = $"Process $(m_commandLineCombo.m_editText)";
	m_isTransmitEnabled = m_state;
	m_actionTable[ActionId.Start].m_text = m_state ? "Detach" : "Start";
	m_actionTable[ActionId.Start].m_icon = m_iconTable[m_state ? IconId.Detach : IconId.Start];
	m_actionTable[ActionId.Terminate].m_isEnabled = m_state;
	m_statusPaneTable[StatusPaneId.State].m_text = m_state ? "Running" : "Idle";
}

//..............................................................................
