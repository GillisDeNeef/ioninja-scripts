//..............................................................................
//
// Process session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_ProcessStream.jnc"
import "ui_History.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class ProcessSession: doc.Session
{
protected:
	enum State
	{
		Closed,
		Opened,
	}

	enum Access
	{
		ReadWrite,
		ReadOnly,
		WriteOnly,
	}

	enum ActionId
	{
		Open,
		Clear,
		_Count,
	}

	enum IconId
	{
		Open,
		Close,
		Clear,
		_Count,
	}

	enum StatusPaneId
	{
		State,
		_Count,
	}

protected:
	// file

	State bindable m_state;
	io.ProcessStream m_file;
	std.Buffer m_readBuffer;
	uint_t m_syncId;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_fileNameCombo;
	ui.ComboBox* m_accessCombo;

	// property grid

	ui.StringProperty* m_fileNameProp;
	ui.EnumProperty* m_accessProp;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode open();
	close();
	clear();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
		);

protected:
	onOpen()
	{
		m_state ? close() : try open();
	}

	onProcessNameComboEnter()
	{
		try open();
	}

	onProcessStreamEvent(
		uint_t syncId,
		io.ProcessStreamEvents triggeredEvents
		);

	io.ProcessStreamOptions getProcessStreamOptions();
	waitProcessStreamEvents();
	createUi();
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

ProcessSession.construct(doc.PluginHost* pluginHost)
{
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
	m_stdSessionInfoSet.addLogListener(pluginHost.m_log);
}

ProcessSession.createUi()
{
	static ui.EnumPropertyOption accessModeTable[] =
	{
		{ "Read/Write",  Access.ReadWrite },
		{ "Read-only",   Access.ReadOnly  },
		{ "Write-only",  Access.WriteOnly },
	}

	// toolbar

	m_pluginHost.m_toolBar.addLabel("Process:");
	m_fileNameCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_fileNameCombo.m_isEditable = true;
	m_fileNameCombo.m_onEnter += onProcessNameComboEnter;
	m_fileNameCombo.m_placeholderText = "Enter a file or device name...";

	m_accessCombo = m_pluginHost.m_toolBar.addComboBox("Access:", accessModeTable, countof(accessModeTable));

	m_iconTable[IconId.Open] = new ui.Icon("images/open.png");
	m_iconTable[IconId.Close] = new ui.Icon("images/close.png");
	m_iconTable[IconId.Clear] = new ui.Icon("images/clear.png");

	m_actionTable[ActionId.Open] = createAction("Open", m_iconTable[IconId.Open]);
	m_actionTable[ActionId.Open].m_onTriggered += onOpen;

	m_actionTable[ActionId.Clear] = createAction("Clear", m_iconTable[IconId.Clear]);
	m_actionTable[ActionId.Clear].m_onTriggered += clear;
	m_actionTable[ActionId.Clear].m_isEnabled = false;

	// status bar

	m_statusPaneTable[StatusPaneId.State] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Connection",
		"Process stream-specific connection settings"
		);

	m_fileNameProp = m_pluginHost.m_propertyGrid.createStringProperty(
		group,,
		"Process name",
		"Enter a file name...",
		"Specify a file name to open"
		);

	m_accessProp = m_pluginHost.m_propertyGrid.createEnumProperty(
		group,,
		"Access mode",
		"Access mode for the file",
		accessModeTable,
		countof(accessModeTable)
		);

	m_bufferPropertySet.createPropertiesGrouped(m_pluginHost.m_propertyGrid);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("Process session");

	m_stdSessionInfoSet.createInformationValues(m_pluginHost.m_informationGrid, infoGroup);
	m_throughputInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);
	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

bool errorcode ProcessSession.open()
{
	close();
	startSession();

	m_fileNameCombo.addEditTextToHistory();

	char const* fileName = m_fileNameCombo.m_editText;

	Access access = m_accessCombo.m_currentData;

	io.ProcessOpenFlags openFlags =
		access == Access.ReadOnly ? io.ProcessOpenFlags.ReadOnly :
		access == Access.WriteOnly ? io.ProcessOpenFlags.WriteOnly : 0;

	m_file.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
	m_file.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
	m_file.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
	m_file.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
	m_file.m_options = getProcessStreamOptions();
	m_file.open(fileName, openFlags);

	char const* p = $"$(m_file.m_kind; B)$fileName";
	m_logWriter.write(ProcessLogRecordCode.Opened, p, dynamic sizeof(p));
	m_state = State.Opened;

	waitProcessStreamEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;

catch:
	std.Error const* error = std.getLastError();
	m_logWriter.write(
		ProcessLogRecordCode.OpenError,
		error,
		error.m_size
		);

	m_file.close();
	return false;
}

ProcessSession.close()
{
	if (m_state != State.Opened)
		return;

	m_logWriter.write(ProcessLogRecordCode.Closed);
	m_file.close();
	m_state = State.Closed;
	m_syncId++;
	m_stdSessionInfoSet.endSession();
}

ProcessSession.clear()
{
	if (m_state != State.Opened)
		return;

	try m_file.clear();
	m_logWriter.write(ProcessLogRecordCode.Cleared);
}

io.ProcessStreamOptions ProcessSession.getProcessStreamOptions()
{
	io.ProcessStreamOptions options;

	if (m_bufferPropertySet.m_keepReadBlockSizeProp.m_value)
		options |= io.ProcessStreamOptions.KeepReadBlockSize;

	if (m_bufferPropertySet.m_keepWriteBlockSizeProp.m_value)
		options |= io.ProcessStreamOptions.KeepWriteBlockSize;

	return options;
}

ProcessSession.waitProcessStreamEvents()
{
	io.ProcessStreamEvents eventMask =
		io.ProcessStreamEvents.IoError |
		io.ProcessStreamEvents.ReadBufferFull |
		io.ProcessStreamEvents.IncomingData |
		io.ProcessStreamEvents.Eof;

	if (!m_isTransmitEnabled)
		eventMask |= io.ProcessStreamEvents.WriteBufferReady;

	m_file.wait(eventMask, onProcessStreamEvent~(++m_syncId) @ m_pluginHost.m_mainThreadScheduler);
}

void ProcessSession.load(doc.Storage* storage)
{
	m_fileNameCombo.loadHistory(storage, "fileNameHistory");
	m_fileNameCombo.m_editText = storage.readString("fileName");
	m_accessCombo.m_currentData = storage.readInt("access");

	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void ProcessSession.save(doc.Storage* storage)
{
	m_bufferPropertySet.save(storage);

	m_fileNameCombo.saveHistory(storage, "fileNameHistory");
	storage.writeString("fileName", m_fileNameCombo.m_editText);
	storage.writeInt("access", m_accessCombo.m_currentData);
}

void ProcessSession.updateProperties()
{
	m_fileNameProp.m_value = m_fileNameCombo.m_editText;
	m_accessProp.m_value = m_accessCombo.m_currentData;
}

bool errorcode ProcessSession.applyProperties()
{
	m_fileNameCombo.m_editText = m_fileNameProp.m_value;
	m_accessCombo.m_currentData = m_accessProp.m_value;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);

	if (!m_file.m_isOpen)
		return true;

	m_file.m_readParallelism = m_bufferPropertySet.m_readParallelismProp.m_value;
	m_file.m_readBlockSize = m_bufferPropertySet.m_readBlockSizeProp.m_value;
	m_file.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
	m_file.m_writeBufferSize = m_bufferPropertySet.m_writeBufferSizeProp.m_value;
	m_file.m_options = getProcessStreamOptions();
	return true;
}

void ProcessSession.restoreDefaultProperties()
{
	m_accessProp.m_value = Access.ReadWrite;
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode ProcessSession.transmit(
	void const* p,
	size_t size
	)
{
	if (m_state != State.Opened)
		return -1;

	size_t result = m_file.write(p, size);
	if (result)
		m_logWriter.write(log.StdRecordCode.Tx, p, result);

	return result;
}

ProcessSession.onProcessStreamEvent(
	uint_t syncId,
	io.ProcessStreamEvents triggeredEvents
	)
{
	if (syncId != m_syncId)
		return;

	if (triggeredEvents & io.ProcessStreamEvents.IoError)
	{
		m_logWriter.write(log.StdRecordCode.Error, m_file.m_ioError, m_file.m_ioError.m_size);
		close();
		return;
	}

	if ((triggeredEvents & io.ProcessStreamEvents.ReadBufferFull) &&
		m_bufferPropertySet.m_notifyReadBufferFullProp.m_value)
		m_logWriter.write(log.StdRecordCode.RxBufferFull);

	if (triggeredEvents & io.ProcessStreamEvents.IncomingData)
	{
		size_t result = m_file.read(m_readBuffer.m_p, m_readBuffer.m_size);
		if (result)
			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
	}

	if (triggeredEvents & io.ProcessStreamEvents.Eof)
	{
		for (;;) // read all the buffered data
		{
			size_t result = m_file.read(m_readBuffer.m_p, m_readBuffer.m_size);
			if (!result)
				break;

			m_logWriter.write(log.StdRecordCode.Rx, m_readBuffer.m_p, result);
		}

		m_logWriter.write(ProcessLogRecordCode.Eof);
		close();
		return;
	}

	if (io.ProcessStreamEvents.WriteBufferReady)
		m_isTransmitEnabled = true;

	waitProcessStreamEvents();
}

reactor ProcessSession.m_uiReactor
{
	m_title = $"Process $(m_fileNameCombo.m_editText)";
	m_isTransmitEnabled = m_state == State.Opened;
	m_accessCombo.m_isEnabled = !m_state;
	m_accessProp.m_isEnabled = !m_state;
	m_actionTable[ActionId.Open].m_text = m_state ? "Close" : "Open";
	m_actionTable[ActionId.Open].m_icon = m_iconTable[m_state ? IconId.Close : IconId.Open];
	m_actionTable[ActionId.Clear].m_isEnabled = m_state && m_file.m_kind == io.ProcessKind.Disk;
	m_statusPaneTable[StatusPaneId.State].m_text = m_state ? "Opened" : "Closed";
}

//..............................................................................
