//..............................................................................
//
// Sniffer session
//

import "doc_Session.jnc"
import "io_base.jncx"
import "io_pcap.jncx"
import "io_IpDefragmenter.jnc"
import "ui_Dialog.jnc"
import "ui_History.jnc"
import "ui_PcapUi.jnc"
import "ui_BufferPropertySet.jnc"
import "ui_StdSessionInfoSet.jnc"
import "log_ThroughputCalc.jnc"
import "log_ChecksumCalc.jnc"
import "std_Buffer.jnc"

using extension ui.ComboBoxHistory;

//..............................................................................

class PcapSnifferSession: doc.Session
{
protected:
	enum Defaults
	{
		Promiscious  = false,
		SnapshotSize = 8 * 1024,
		ReadTimeout  = 200,
	}

	enum State
	{
		Closed,
		Capturing,
	}

	enum ActionId
	{
		Capture,
		_Count,
	}

	enum IconId
	{
		Capture,
		StopCapture,
		_Count,
	}

	enum StatusPaneId
	{
		State,
		_Count,
	}

protected:
	// pcap

	State bindable m_state;
	io.Pcap m_pcap;
	io.IpDefragmenter m_ipDefragmenter;
	std.Buffer m_readBuffer;
	uint_t m_syncId;

	NetSnifferLogFilter m_filter;

	// toolbar, status bar & menu

	ui.Action* m_actionTable[ActionId._Count];
	ui.Icon* m_iconTable[IconId._Count];
	ui.StatusPane* m_statusPaneTable[StatusPaneId._Count];
	ui.ComboBox* m_deviceCombo;
	ui.ComboBox* m_captureFilterCombo;
	ui.ComboBox* m_viewFilterCombo;
	ui.FileDlg* m_fileDlg;

	// property grid

	ui.EnumProperty* m_deviceProp;
	ui.BoolProperty* m_promisciousProp;
	ui.StringProperty* m_captureFilterProp;
	ui.StringProperty* m_viewFilterProp;
	ui.IntProperty* m_ipFragmentLimitProp;
	ui.IntProperty* m_ipFragmentTimeoutProp;
	ui.IntProperty* m_snapshotSizeProp;
	ui.BufferPropertySet m_bufferPropertySet;

	// information grid

	ui.StdSessionInfoSet m_stdSessionInfoSet;
	ui.ThroughputInfoSet m_throughputInfoSet;
	ui.ChecksumInfoSet m_checksumInfoSet;

	// ui reactor

	reactor m_uiReactor;

public:
	construct(doc.PluginHost* pluginHost);

	bool errorcode capture();

	bool errorcode capture(
		io.PcapDeviceDesc const* device,
		char const* filterString
		);

	bool errorcode openCapFile(
		char const* fileName,
		char const* filterString
		);

	stopCapture();

	override void load(doc.Storage* storage);
	override void save(doc.Storage* storage);
	override void updateProperties();
	override bool errorcode applyProperties();
	override void restoreDefaultProperties();

	override size_t errorcode transmit(
		void const* p,
		size_t size
		);

protected:
	onCapture()
	{
		try(m_state ? stopCapture() : capture());
	}

	onSetCaptureFilter()
	{
		try(m_state ? applyCaptureFilter() : capture());
	}

	onSetViewFilter()
	{
		try applyViewFilter(true);
	}

	onDeviceComboEnter()
	{
		try capture();
	}

	onFileDlgCompleted(ui.StdDlgButton button);

	onPcapEvent(
		uint_t syncId,
		io.PcapEvents triggeredEvents
		);

	bool errorcode applyCaptureFilter();
	bool errorcode applyViewFilter(bool rebuildLog = false);

	processPacket(
		void const* p,
		size_t size
		);

	waitPcapEvents();
	createUi();
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

PcapSnifferSession.construct(doc.PluginHost* pluginHost)
{
	basetype.construct(pluginHost);

	createUi();

	pluginHost.m_log.addSelectionProcessor(log.ThroughputCalcRangeProcessor.factory~(m_throughputInfoSet));
	pluginHost.m_log.addSelectionProcessor(log.ChecksumCalcRangeProcessor.factory~(m_checksumInfoSet));
	pluginHost.m_log.addFilter(m_filter);
}

PcapSnifferSession.createUi()
{
	// toolbar

	m_pluginHost.m_toolBar.addLabel("Device:");
	m_deviceCombo = ui.createPcapDeviceComboBox(m_pluginHost.m_toolBar);
	m_deviceCombo.m_onEnter += onDeviceComboEnter;

	m_pluginHost.m_toolBar.addLabel("Capture filter:");
	m_captureFilterCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_captureFilterCombo.m_isEditable = true;
	m_captureFilterCombo.m_onEnter += onSetCaptureFilter;

	m_iconTable[IconId.Capture] = new ui.Icon("images/capture.png");
	m_iconTable[IconId.StopCapture] = new ui.Icon("images/pause.png");

	m_actionTable[ActionId.Capture] = createAction("Capture", m_iconTable [IconId.Capture]);
	m_actionTable[ActionId.Capture].m_onTriggered += onCapture;

	m_pluginHost.m_toolBar.addLabel("View filter:");
	m_viewFilterCombo = m_pluginHost.m_toolBar.addComboBox(-1);
	m_viewFilterCombo.m_isEditable = true;
	m_viewFilterCombo.m_onEnter += onSetViewFilter;

	// status bar

	m_statusPaneTable[StatusPaneId.State] = m_pluginHost.m_statusBar.addPane(, 80);

	// property grid

	ui.GroupProperty* group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Connection",
		"Pcap-specific connection settings"
		);

	m_deviceProp = ui.createPcapDeviceProperty(
		m_pluginHost.m_propertyGrid,
		group,,,
		"Device",
		"Specify the device to capture on"
		);

	m_promisciousProp = m_pluginHost.m_propertyGrid.createBoolProperty(
		group,,
		"Promiscious mode",
		"Put the capturing interface into promiscious mode"
		);

	m_captureFilterProp = m_pluginHost.m_propertyGrid.createStringProperty(
		group,,
		"Capture filter",
		"Capture only the packets which have passed this filter"
		);

	m_viewFilterProp = m_pluginHost.m_propertyGrid.createStringProperty(
		group,,
		"View filter",
		"View only the packets which have passed this filter"
		);

	group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"IP defragmenter",
		"IP defragmentation settings"
		);

	m_ipFragmentLimitProp  = m_pluginHost.m_propertyGrid.createIntProperty(
		group,,
		"IP fragment limit",
		"Specify the maximum number of IP fragments"
		);

	m_ipFragmentTimeoutProp  = m_pluginHost.m_propertyGrid.createIntProperty(
		group,,
		"IP fragment timeout (ms)",
		"Specify the maximum delay between IP fragments"
		);

	group = m_pluginHost.m_propertyGrid.createGroupProperty(
		"Buffering",
		"Advanced buffering options affecting throughtput and compatibility"
		);

	m_snapshotSizeProp = m_pluginHost.m_propertyGrid.createIntProperty(
		group,,
		"Snapshot size",
		"Specify the Pcap snapshot size"
		);

	m_snapshotSizeProp.m_spinBoxStep = 1024;

	m_bufferPropertySet.createProperties(
		m_pluginHost.m_propertyGrid,
		group,,
		ui.BufferPropertySet.PropertyId.ReadBufferSize |
		ui.BufferPropertySet.PropertyId.NotifyReadBufferFull
		);

	// information grid

	ui.InformationGroup* infoGroup = m_pluginHost.m_informationGrid.createGroup("Network sniffer");

	m_stdSessionInfoSet.createInformationValues(
		m_pluginHost.m_informationGrid,
		infoGroup,,
		ui.StdSessionInfoSet.InfoId.SessionTime |
		ui.StdSessionInfoSet.InfoId.TotalSize
		);

	m_throughputInfoSet.createInformationValuesGrouped(
		m_pluginHost.m_informationGrid,,,
		ui.ThroughputInfoSet.InfoId.TimeSpan |
		ui.ThroughputInfoSet.InfoId.TotalSize |
		ui.ThroughputInfoSet.InfoId.TotalThroughput
		);

	m_checksumInfoSet.createInformationValuesGrouped(m_pluginHost.m_informationGrid);

	m_pluginHost.m_informationGrid.resizeNameColumnToContents();

	m_uiReactor.start();
}

PcapSnifferSession.onFileDlgCompleted(ui.StdDlgButton button)
{
	if (button != ui.StdDlgButton.Ok)
		return;

	char const* fileName = m_fileDlg.m_filePath;
	char const* filterString = m_captureFilterCombo.m_editText;
	m_captureFilterCombo.addEditTextToHistory();

	try openCapFile(fileName, filterString);
}

bool errorcode PcapSnifferSession.capture()
{
	stopCapture();

	if (m_deviceCombo.m_currentIndex == -1)
		return false;

	io.PcapDeviceDesc const* device = m_deviceCombo.m_currentData;
	if (!device)
	{
		if (!m_fileDlg)
		{
			m_fileDlg = new ui.FileDlg;
			m_fileDlg.m_onCompleted += onFileDlgCompleted;
		}

		m_fileDlg.m_kind = ui.FileDlgKind.Open;
		m_fileDlg.m_title = "Open Wireshark File (*.cap; *.pcap)...";
		m_fileDlg.m_filter = "Wireshark .cap files (*.cap; *.pcap);; All files (*.*)";
		m_fileDlg.show();
		return true;
	}

	char const* filterString = m_captureFilterCombo.m_editText;
	m_captureFilterCombo.addEditTextToHistory();

	return try capture(device, filterString);
}

bool errorcode PcapSnifferSession.capture(
	io.PcapDeviceDesc const* device,
	char const* filterString
	)
{
	stopCapture();
	startSession();

	writeLogString(PcapLogRecordCode.StartingCapture, filterString);

	m_pcap.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;

	bool result = try m_pcap.openDevice(
		device.m_name,
		filterString,
		m_snapshotSizeProp.m_value,
		m_promisciousProp.m_value,
		Defaults.ReadTimeout
		);

	if (!result)
	{
		writeLogLastError(PcapLogRecordCode.CaptureError);
		return false;
	}

	m_state = State.Capturing;
	writeLog(PcapLogRecordCode.CaptureStarted);
	waitPcapEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	applyViewFilter();
	return true;
}

bool errorcode PcapSnifferSession.openCapFile(
	char const* fileName,
	char const* filterString
	)
{
	writeLogString(PcapLogRecordCode.StartingCapture, filterString);

	m_pcap.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;

	bool result = try m_pcap.openFile(
		fileName,
		filterString
		);

	if (!result)
	{
		writeLogLastError(PcapLogRecordCode.CaptureError);
		return false;
	}

	m_state = State.Capturing;
	writeLog(PcapLogRecordCode.CaptureStarted);
	waitPcapEvents();
	m_stdSessionInfoSet.startSession(m_pluginHost.m_mainThreadScheduler);
	return true;
}

PcapSnifferSession.stopCapture()
{
	if (!m_state)
		return;

	writeLog(PcapLogRecordCode.CaptureStopped);
	m_pcap.close();
	m_state = State.Closed;
	m_syncId++;
	m_stdSessionInfoSet.endSession();
}

bool errorcode PcapSnifferSession.applyCaptureFilter()
{
	if (!m_state)
	{
		m_captureFilterCombo.addEditTextToHistory();
		return true;
	}

	char const* filter = m_captureFilterCombo.m_editText;
	writeLogString(PcapLogRecordCode.ChangingCaptureFilter, filter);
	bool result = try m_pcap.setFilter(filter);
	if (!result)
	{
		writeLogLastError(PcapLogRecordCode.CaptureFilterError);
		return false;
	}

	writeLog(PcapLogRecordCode.CaptureFilterChanged);
	m_captureFilterCombo.addEditTextToHistory();
	return true;
}

bool errorcode PcapSnifferSession.applyViewFilter(bool rebuildLog)
{
	m_pluginHost.m_log.suspend();

	char const* filter = m_viewFilterCombo.m_editText;

	bool result = m_state ?
		try m_filter.setFilter(m_pcap, filter) :
		try m_filter.setFilter(filter);

	m_pluginHost.m_log.resume();

	if (!result)
	{
		m_viewFilterCombo.m_backgroundColor = ui.StdColor.PastelRed;
		return false;
	}

	m_viewFilterCombo.m_backgroundColor = ui.ColorFlags.Transparent;
	m_viewFilterCombo.addEditTextToHistory();

	if (rebuildLog)
		m_pluginHost.m_log.rebuild();

	return true;
}

PcapSnifferSession.waitPcapEvents()
{
	io.PcapEvents eventMask =
		io.PcapEvents.IoError |
		io.PcapEvents.ReadBufferFull |
		io.PcapEvents.IncomingData |
		io.PcapEvents.Eof;

	m_pcap.wait(eventMask, onPcapEvent ~(++m_syncId) @ m_pluginHost.m_mainThreadScheduler);
}

void PcapSnifferSession.load(doc.Storage* storage)
{
	m_captureFilterCombo.loadHistory(storage, "captureFilterHistory");
	m_viewFilterCombo.loadHistory(storage, "viewFilterHistory");
	m_deviceCombo.m_currentIndex = storage.readInt("deviceIdx");
	m_captureFilterCombo.m_editText = storage.readString("captureFilter");
	m_viewFilterCombo.m_editText = storage.readString("viewFilter");
	m_promisciousProp.m_value = storage.readBool("promiscious", Defaults.Promiscious);
	m_ipFragmentLimitProp.m_value = storage.readInt("ipFragmentLimit", io.IpDefragmenter.DefIpFragmentLimit);
	m_ipFragmentTimeoutProp.m_value = storage.readInt("ipFragmentTimeout", io.IpDefragmenter.DefIpFragmentTimeout);
	m_snapshotSizeProp.m_value = storage.readInt("snapshotSize", Defaults.SnapshotSize);

	m_bufferPropertySet.load(storage);
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);
}

void PcapSnifferSession.save(doc.Storage* storage)
{
	m_bufferPropertySet.save(storage);
	m_captureFilterCombo.saveHistory(storage, "captureFilterHistory");
	m_viewFilterCombo.saveHistory(storage, "viewFilterHistory");
	storage.writeInt("deviceIdx", (int) m_deviceCombo.m_currentIndex);
	storage.writeString("captureFilter", m_captureFilterCombo.m_editText);
	storage.writeString("viewFilter", m_viewFilterCombo.m_editText);
	storage.writeBool("promiscious", m_promisciousProp.m_value);
	storage.writeInt("ipFragmentLimit", m_ipFragmentLimitProp.m_value);
	storage.writeInt("ipFragmentTimeout", m_ipFragmentTimeoutProp.m_value);
	storage.writeInt("snapshotSize", m_snapshotSizeProp.m_value);
}

void PcapSnifferSession.updateProperties()
{
	m_deviceProp.m_currentIndex = m_deviceCombo.m_currentIndex;
	m_captureFilterProp.m_value = m_captureFilterCombo.m_editText;
	m_viewFilterProp.m_value = m_viewFilterCombo.m_editText;
}

bool errorcode PcapSnifferSession.applyProperties()
{
	m_deviceCombo.m_currentIndex = m_deviceProp.m_currentIndex;
	m_captureFilterCombo.m_editText = m_captureFilterProp.m_value;
	m_viewFilterCombo.m_editText = m_viewFilterProp.m_value;
	m_ipDefragmenter.m_fragmentLimit = m_ipFragmentLimitProp.m_value;
	m_ipDefragmenter.m_timeout = m_ipFragmentTimeoutProp.m_value;
	m_readBuffer.setSize(m_bufferPropertySet.m_readBufferSizeProp.m_value);

	applyViewFilter();

	if (!m_pcap.m_isOpen)
		return true;

	m_pcap.m_readBufferSize = m_bufferPropertySet.m_readBufferSizeProp.m_value;
	applyCaptureFilter();
	return true;
}

void PcapSnifferSession.restoreDefaultProperties()
{
	m_deviceProp.m_currentIndex = 0;
	m_promisciousProp.m_value = Defaults.Promiscious;
	m_snapshotSizeProp.m_value = Defaults.SnapshotSize;
	m_ipFragmentLimitProp.m_value = io.IpDefragmenter.DefIpFragmentLimit;
	m_ipFragmentTimeoutProp.m_value = io.IpDefragmenter.DefIpFragmentTimeout;
	m_bufferPropertySet.restoreDefaults();
}

size_t errorcode PcapSnifferSession.transmit(
	void const* p,
	size_t size
	)
{
	if (!m_pcap)
		return -1;

	size_t result = try m_pcap.write(p, size);
	if (result != -1)
	{
		writeLog(NetSnifferLogRecordCode.InjectedPacket, p, result);
		m_stdSessionInfoSet.addTxData(size);
	}

	return result;
}

PcapSnifferSession.onPcapEvent(
	uint_t syncId,
	io.PcapEvents triggeredEvents
	)
{
	if (syncId != m_syncId)
		return;

	if (triggeredEvents & io.PcapEvents.IoError)
	{
		writeLog(log.StdRecordCode.Error, m_pcap.m_ioError, m_pcap.m_ioError.m_size);
		stopCapture();
		return;
	}

	if ((triggeredEvents & io.PcapEvents.ReadBufferFull) &&
		m_bufferPropertySet.m_notifyReadBufferFullProp.m_value)
		writeLog(log.StdRecordCode.RxBufferFull);

	if (triggeredEvents & io.PcapEvents.IncomingData)
	{
		size_t result = m_pcap.read(m_readBuffer.m_p, m_readBuffer.m_size);
		if (result)
			processPacket(m_readBuffer.m_p, result);
	}

	if (triggeredEvents & io.PcapEvents.Eof)
	{
		stopCapture();
		return;
	}

	waitPcapEvents();
}

PcapSnifferSession.processPacket(
	void const* p,
	size_t size
	)
{
	writeLog(NetSnifferLogRecordCode.Packet, p, size);
	m_stdSessionInfoSet.addRxData(size);

	// defragment IPv4

	if (size < sizeof(io.EthernetHdr) + sizeof(io.IpHdr))
		return;

	io.EthernetHdr const* ethernetHdr = (io.EthernetHdr const*) p;
	if (ethernetHdr.m_type != io.EthernetType.Ip)
		return;

	p += sizeof(io.EthernetHdr);
	size -= sizeof(io.EthernetHdr);

	io.IpHdr const* ipHdr = (io.IpHdr const*) p;
	if ((ipHdr.m_flags & io.IpFlags.Mf) || ipHdr.m_fragmentOffset)
	{
		ipHdr = m_ipDefragmenter.defragment(ipHdr, size);
		if (ipHdr)
		{
			size = dynamic sizeof(ipHdr);
			writeLog(NetSnifferLogRecordCode.DefragmentedIp, ipHdr, size);
		}
	}
}

reactor PcapSnifferSession.m_uiReactor
{
	m_title = $"Pcap $(m_deviceCombo.m_currentText)";
	m_isTransmitEnabled = m_state == State.Capturing;
	m_actionTable[ActionId.Capture].m_text = m_state ? "Stop Capture" : "Capture";
	m_actionTable[ActionId.Capture].m_icon = m_iconTable[m_state ? IconId.StopCapture : IconId.Capture];
	m_statusPaneTable[StatusPaneId.State].m_text = m_state ? "Capturing" : "Closed";
}

//..............................................................................
