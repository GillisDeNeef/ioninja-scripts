//..............................................................................
//
// Network Sniffer log filter
//

import "log_Filter.jnc"
import "io_pcap.jncx"

//..............................................................................

class NetSnifferLogFilter: log.Filter
{
protected:
	io.PcapFilter m_pcapFilter;

public:
	// suspend log engine before calling setFilter

	bool errorcode setFilter(
		io.Pcap* pcap,
		char const* filter,
		bool isOptimized = true,
		uint32_t netMask = -1
		)
	{
		return m_pcapFilter.compile(pcap, filter, isOptimized, netMask);
	}

	bool errorcode setFilter(
		char const* filter,
		bool isOptimized = true,
		uint32_t netMask = -1
		)
	{
		return m_pcapFilter.compile(filter, isOptimized, netMask);
	}

protected:
	override bool filter(
		uint64_t timestamp,
		uint64_t recordCode,
		void const* p,
		size_t size
		);
}

// . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool NetSnifferLogFilter.filter(
	uint64_t timestamp,
	uint64_t recordCode,
	void const* p,
	size_t size
	)
{
	switch (recordCode)
	{
	case NetSnifferLogRecordCode.Packet:
	case NetSnifferLogRecordCode.InjectedPacket:
		return m_pcapFilter.match(p, size);

	default:
		return true; // everything else is visible
	}
}

//..............................................................................
