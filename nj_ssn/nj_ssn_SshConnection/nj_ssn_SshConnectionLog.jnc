//.............................................................................
//
// SSH Connection log packet declarations and representer
//

//.............................................................................

enum SshConnectionLogPacketCode
{
	Connecting       = 1 | log.PacketCodeFlags.Mergeable,
	ConnectCompleted = 2 | log.PacketCodeFlags.Mergeable,
	ConnectCancelled = 3 | log.PacketCodeFlags.Mergeable,
	ConnectError     = 4 | log.PacketCodeFlags.Mergeable,
	Disconnected     = 5,
}

enum SshConnectionPartCode
{
	Connecting       = 1 | log.PacketCodeFlags.MergeableForward,
	ConnectCompleted = 1 | log.PacketCodeFlags.MergeableBackward,
}

//.............................................................................

class SshConnectionLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t packetCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);

	static SshConnectionLogRepresenter* getSingleton ()
	{
		static SshConnectionLogRepresenter singleton;
		return &singleton;
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool SshConnectionLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t packetCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (packetCode)
	{
	case SshConnectionLogPacketCode.Connecting:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshConnectionPartCode.Connecting, $"Connecting to <15>$addressString<>...");
		break;

	case SshConnectionLogPacketCode.ConnectCompleted:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		target.m_lineAttr.m_backColor = log.StdLogColor.Start;
		target.addHyperText (SshConnectionPartCode.ConnectCompleted, "ok");
		break;

	case SshConnectionLogPacketCode.ConnectCancelled:
		target.addHyperText (SshConnectionPartCode.ConnectCompleted, "cancelled");
		break;

	case SshConnectionLogPacketCode.ConnectError:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;

		jnc.Error* Error = (jnc.Error*) p;
		target.addHyperText (SshConnectionPartCode.ConnectCompleted, $"error ($(Error.m_description))");
		break;

	case SshConnectionLogPacketCode.Disconnected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
		target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
		target.addHyperText ($"Disconnected from <15>$addressString<>");
		break;

	default:
		return false;
	}

	return true;
}

//.............................................................................
