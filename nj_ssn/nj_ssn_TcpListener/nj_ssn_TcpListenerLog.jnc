//.............................................................................
//
// TCP Listener log packet declarations and representer
//

//.............................................................................

enum TcpListenerLogPacketCode
{
	Listening          = 1,
	ListenError        = 2,
	ListenStopped      = 3,
	ClientConnected    = 4,
	ClientDisconnected = 5,
	ClientRejected     = 6,
}

//.............................................................................

class TcpListenerLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t packetCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);

	static TcpListenerLogRepresenter* getSingleton ()
	{
		static TcpListenerLogRepresenter singleton;
		return &singleton;
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool TcpListenerLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t packetCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (packetCode)
	{
	case TcpListenerLogPacketCode.Listening:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		target.m_lineAttr.m_backColor = log.StdLogColor.Start;
		target.addHyperText ($"Listening on <15>$addressString<>");
		break;

	case TcpListenerLogPacketCode.ListenError:
		jnc.Error const* Error = (jnc.Error const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;
		target.addHyperText ($"Cannot listen ($(Error.m_description))");
		break;

	case TcpListenerLogPacketCode.ListenStopped:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
		target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
		target.addHyperText ($"Stopped listening on <15>$addressString<>");
		break;

	case TcpListenerLogPacketCode.ClientConnected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ($"Client connected from <15>$addressString<>");
		break;

	case TcpListenerLogPacketCode.ClientDisconnected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ($"Client <15>$addressString<> disconnected");
		break;

	case TcpListenerLogPacketCode.ClientRejected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Warning;
		target.m_lineAttr.m_backColor = log.StdLogColor.Warning;
		target.addHyperText ($"Client <15>$addressString<> is rejected ");
		break;

	default:
		return false;
	}

	return true;
}

//.............................................................................
