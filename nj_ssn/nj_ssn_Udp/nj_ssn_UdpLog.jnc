enum RecordCode
{
	Error,
	Open,
	Close,
	Tx,
	Rx,
}

class UdpLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t recordCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool UdpLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t recordCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (recordCode)
	{
		case RecordCode.Error:
			target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
			target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
			target.m_lineAttr.m_backColor = log.StdLogColor.Error;

			jnc.Error const* Error = (jnc.Error const*) p;
			target.addHyperText ($"error ($(Error.m_description))");
			break;
			
		case RecordCode.Open:
			target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
			target.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
			target.m_lineAttr.m_backColor = log.StdLogColor.Start;
			target.addHyperText($"Opened socket at <#0000FF>$((char const*) p)<>");
			break;
			
		case RecordCode.Close:
			target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
			target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
			target.addHyperText($"Socket at <#0000FF>$((char const*) p)<> closed");
			break;
			
		case RecordCode.Tx:
			target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
			target.m_lineAttr.m_iconIdx = log.StdLogIcon.Tx;
			target.m_lineAttr.m_textColor = gui.StdColor.Blue;
			target.addBin (p, size);
			break;
			
		case RecordCode.Rx:
			target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
			target.m_lineAttr.m_iconIdx = log.StdLogIcon.Rx;
			target.m_lineAttr.m_textColor = gui.StdColor.Green;
			target.addBin (p, size);
			break;
			
		default:
			target.addHyperText((char const*) p);
	}
	
	return true;
}