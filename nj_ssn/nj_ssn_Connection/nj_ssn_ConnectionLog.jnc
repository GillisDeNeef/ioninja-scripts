//.............................................................................
//
// Connection log packet declarations and representer
//

//.............................................................................

enum ConnectionLogPacketCode
{
	Connecting       = 1 | log.PacketCodeFlags.Mergeable,
	ConnectCompleted = 2 | log.PacketCodeFlags.Mergeable,
	ConnectCancelled = 3 | log.PacketCodeFlags.Mergeable,
	ConnectError     = 4 | log.PacketCodeFlags.Mergeable,
	Disconnected     = 5,
	Tx               = 6 | log.PacketCodeFlags.Mergeable,
	Rx               = 7 | log.PacketCodeFlags.Mergeable,
}

enum ConnectionPartCode
{
	Connecting       = 1 | log.PacketCodeFlags.MergeableForward,
	ConnectCompleted = 1 | log.PacketCodeFlags.MergeableBackward,
}

//.............................................................................

class ConnectionLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t packetCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);

	static ConnectionLogRepresenter* getSingleton ()
	{
		static ConnectionLogRepresenter singleton;
		return &singleton;
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool ConnectionLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t packetCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (packetCode)
	{
	case ConnectionLogPacketCode.Connecting:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (ConnectionPartCode.Connecting, $"Connecting to <15>$addressString<>...");
		break;

	case ConnectionLogPacketCode.ConnectCompleted:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		target.m_lineAttr.m_backColor = log.StdLogColor.Start;
		target.addHyperText (ConnectionPartCode.ConnectCompleted, "ok");
		break;

	case ConnectionLogPacketCode.ConnectCancelled:
		target.addHyperText (ConnectionPartCode.ConnectCompleted, "cancelled");
		break;

	case ConnectionLogPacketCode.ConnectError:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;

		jnc.Error* Error = (jnc.Error*) p;
		target.addHyperText (ConnectionPartCode.ConnectCompleted, $"error ($(Error.m_description))");
		break;

	case ConnectionLogPacketCode.Disconnected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
		target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
		target.addHyperText ($"Disconnected from <15>$addressString<>");
		break;

	case ConnectionLogPacketCode.Tx:
		target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Tx;
		target.m_lineAttr.m_textColor = gui.StdColor.Blue;
		target.addBin (p, size);
		break;

	case ConnectionLogPacketCode.Rx:
		target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Rx;
		target.m_lineAttr.m_textColor = gui.StdColor.Green;
		target.addBin (p, size);
		break;

	default:
		return false;
	}

	return true;
}

//.............................................................................
