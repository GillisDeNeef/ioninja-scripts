//.............................................................................
//
// Listener log packet declarations and representer
//

//.............................................................................

enum ListenerLogPacketCode
{
	Listening          = 1,
	ListenError        = 2,
	ListenStopped      = 3,
	ClientConnected    = 4,
	ClientDisconnected = 5,
	Tx                 = 6 | log.PacketCodeFlags.Mergeable,
	Rx                 = 7 | log.PacketCodeFlags.Mergeable,
}

//.............................................................................

class ListenerLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t packetCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);

	static ListenerLogRepresenter* getSingleton ()
	{
		static ListenerLogRepresenter singleton;
		return &singleton;
	}
}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool ListenerLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t packetCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (packetCode)
	{
	case ListenerLogPacketCode.Listening:
		char const* address = (char const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Play;
		target.m_lineAttr.m_backColor = log.StdLogColor.Start;
		target.addHyperText ($"Listening on <15>$address<>...");
		break;

	case ListenerLogPacketCode.ListenError:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;

		jnc.Error* Error = (jnc.Error*) p;
		target.addHyperText ($"Cannot listen ($(Error.m_description))");
		break;

	case ListenerLogPacketCode.ListenStopped:
		char const* address = (char const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
		target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
		target.addHyperText ($"Stopped listening on <15>$address<>");
		break;

	case ListenerLogPacketCode.ClientConnected:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ("Client connected");
		break;

	case ListenerLogPacketCode.ClientDisconnected:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText ("Client disconnected");
		break;

	case ListenerLogPacketCode.Tx:
		target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Tx;
		target.m_lineAttr.m_foreColor = gui.StdColor.Blue;
		target.addBin (p, size);
		break;

	case ListenerLogPacketCode.Rx:
		target.m_lineAttr.m_flags = log.LineAttrFlags.TileIcon;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Rx;
		target.m_lineAttr.m_foreColor = gui.StdColor.Green;
		target.addBin (p, size);
		break;

	default:
		return false;
	}

	return true;
}

//.............................................................................
