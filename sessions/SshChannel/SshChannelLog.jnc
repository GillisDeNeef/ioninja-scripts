//.............................................................................
//
// SSH Connection log packet declarations and representer
//

//.............................................................................

enum SshChannelLogRecordCode
{
	Resolving             = 1  | log.RecordCodeFlags.Mergeable,
	ResolveCompleted      = 2  | log.RecordCodeFlags.Mergeable,
	TcpConnecting         = 3  | log.RecordCodeFlags.Mergeable,
	TcpConnectCompleted   = 4  | log.RecordCodeFlags.Mergeable,
	SshHandshakeCompleted = 5  | log.RecordCodeFlags.Mergeable,
	SshAuthCompleted      = 6  | log.RecordCodeFlags.Mergeable,
	SshChannelOpened      = 7  | log.RecordCodeFlags.Mergeable,
	SshPtyRequested       = 8  | log.RecordCodeFlags.Mergeable,
	SshProcessStarted     = 9  | log.RecordCodeFlags.Mergeable,
	ConnectCancelled      = 10 | log.RecordCodeFlags.Mergeable,
	ConnectCompleted      = 11 | log.RecordCodeFlags.Mergeable,
	ConnectError          = 12 | log.RecordCodeFlags.Mergeable,
	Disconnected          = 13,
}

enum SshChannelLogPartCode
{
	Connecting       = 1 | log.RecordCodeFlags.MergeableForward,
	ConnectCompleted = 1 | log.RecordCodeFlags.MergeableBackward,
}

//.............................................................................

class SshChannelLogRepresenter: log.Representer
{
	override bool represent (
		log.RepresenterTarget* target,
		uint_t recordCode,
		void const* p,
		size_t size,
		uint64_t foldFlags
		);

}

//. . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . . .

bool SshChannelLogRepresenter.represent (
	log.RepresenterTarget* target,
	uint_t recordCode,
	void const* p,
	size_t size,
	uint64_t foldFlags
	)
{
	switch (recordCode)
	{
	case SshChannelLogRecordCode.Resolving:
		char const* addressString = (char const*) p;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.Connecting, $"Resolving <15>$addressString<>...");
		break;

	case SshChannelLogRecordCode.ResolveCompleted:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, $"ok (<15>$addressString<>)");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Establishing TCP connection...");
		break;

	case SshChannelLogRecordCode.TcpConnecting:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.Connecting, $"Establishing TCP connection to <15>$addressString<>...");
		break;

	case SshChannelLogRecordCode.TcpConnectCompleted:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Performing SSH handshake...");
		break;

	case SshChannelLogRecordCode.SshHandshakeCompleted:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Performing SSH authentification...");
		break;

	case SshChannelLogRecordCode.SshAuthCompleted:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Opening SSH channel...");
		break;

	case SshChannelLogRecordCode.SshChannelOpened:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Requesting PTY...");
		break;

	case SshChannelLogRecordCode.SshPtyRequested:
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Info;
		target.m_lineAttr.m_backColor = log.StdLogColor.Info;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		target.addHyperText (SshChannelLogPartCode.Connecting, "Starting process...");
		break;

	case SshChannelLogRecordCode.SshProcessStarted:
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "ok");
		break;

	case SshChannelLogRecordCode.ConnectCompleted:
		target.m_lineAttr.m_iconIdx  = log.StdLogIcon.Play;
		target.m_lineAttr.m_backColor = log.StdLogColor.Start;
		target.addHyperText ("SSH channel established");
		break;

	case SshChannelLogRecordCode.ConnectCancelled:
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, "cancelled");
		break;

	case SshChannelLogRecordCode.ConnectError:
		target.m_lineAttr.m_flags = log.LineAttrFlags.Override;
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Error;
		target.m_lineAttr.m_backColor = log.StdLogColor.Error;

		jnc.Error const* Error = (jnc.Error const*) p;
		target.addHyperText (SshChannelLogPartCode.ConnectCompleted, $"error ($(Error.m_description))");
		break;

	case SshChannelLogRecordCode.Disconnected:
		char const* addressString = try io.formatSocketAddress ((io.SocketAddress const*) p);
		target.m_lineAttr.m_iconIdx = log.StdLogIcon.Pause;
		target.m_lineAttr.m_backColor = log.StdLogColor.Stop;
		target.addHyperText ($"Disconnected from <15>$addressString<>");
		break;

	default:
		return false;
	}

	return true;
}

//.............................................................................
